#!/usr/bin/perl -w
#
# templateer - Automates processing of template files
#
#   Simply create
# 
# Written by Wil Cooley <wcooley@nakedape.cc>
#
# Copyright (C) 2004 Naked Ape Consulting, Ltd.
#

use 5.008000;
use strict;
use warnings;
use diagnostics;
use Getopt::Long qw/VersionMessage :config default no_ignore_case/;
use Pod::Usage;
use Text::Templateer qw(:all);

our $VERSION = '1.000_00';

my %instance_data;
my $instance_text;

GetOptions(
    'append|a'      =>  sub { set_flag('append')    },
    'overwrite|o'   =>  sub { clear_flag('append')  },

    'list|l'        =>  sub { set_flag('list_only') },

    'verbose|v'     =>  sub { set_flag('verbose')   },
    'quiet|q'       =>  sub { clear_flag('verbose') },

    'ask|prompt|p'  =>  sub { set_flag('prompt')    },

    'help|h'        =>  sub { pod2usage(0);         },
    'version|V'     =>  sub { VersionMessage(0);    },
) or pod2usage(2);

my $template    = $ARGV[0];
my $outfile     = $ARGV[1] unless (is_flagged('list_only'));

my $template_text = read_template($template);
my @keywords = find_keywords_in($template_text);

# "list only" mode allows you to just list defined template variables
if (is_flagged('list_only')) {
    print "The template ${template} defines the following variables:\n";
    show_keywords(\@keywords);
    exit 0;
}

@keywords = fill_keywords_from_env(\@keywords, \%instance_data);

prompt_user_for_keys(\@keywords, \%instance_data)
    if (@keywords);

$instance_text = fill_template_text($template_text, \%instance_data);

show_instance_data(\%instance_data);

if (is_flagged('prompt')) {
    exit 0 unless prompt_for_ok();
}

write_instance_file($outfile, $instance_text);



__END__

=head1 NAME

templateer - Very simple templating utility.

=head1 SYNOPSIS

templateer [options] <template_file> <output_file>

Options:

    -l --list           List template variables in template

    -a --append         Append to output file
    -o --overwrite      Overwrite output file
    
    -v --verbose        Be verbose
    -q --quiet          Be not verbose

    -p --ask, --prompt  Prompt for confirmation before writing output

    -h --help           Get help
    -V --version        Display version



=head1 DESCRIPTION

B<templateer> is a quick and easy templating script which has proven to
be tremendously useful to me.  It reads a template file, extracts a list of
template variables, finds values for the variables and then writes the fill-in
instance file.  It will first check the environment for variables with the
name given in the template with 'TPL_' prepended (to avoid clashes with other
environment variables).  If not found in the environment, the user will be
prompted (on stderr, so stdout is left virginal for the template instance).

Template variables are just strings delineated with '@@' on each end and
on one line.  There are no plans to do much more than that; it is not a
replacement for a full-blown templating system like Template Toolkit, just
a limited-use, quick and easy templating script.  (The only improvement I'm
currently considering is to allow the delimiter to be set, to be useful in
those rare cases where '@@' is required otherwise.  I will probably fix it
if I run into one of those cases.  I have not yet.)

The file name '-' can also be used for either template or instance file
(or both), in which case it has the usual meaning --- read from stdin and
write to stdout.  However, if it is to read the template from standard input,
it *must* be provided all values by the environment, because, well, how else
would you read in the input?

B<templateer> works even if input and output files are the same; the input
file is fully read before the output is written.

Here's how you might use it.  Like most people who host web sites with
Apache, you've probably got a whole lot of sites that are pretty much the
same configuration (and for various reasons you're not using mod_vhost_alias)
and you add enough new sites that creating the configuration is tedious and
probably somewhat error-prone.  So you make a template file, 'vhost.tpl',
like this:

    <VirtualHost @@VIP_ADDR@@:80>
        ServerName @@DOMAIN@@
        ServerAlias *.@@DOMAIN@@
        DocumentRoot /var/www/vhosts/@@DOMAIN@@/htdocs/
        CustomLog /var/www/vhosts/@@DOMAIN@@/logs/access_log combined
        ErrorLog /var/www/vhosts/@@DOMAIN@@/logs/error_log
    </VirtualHost>

Now you can use this template in several ways with B<templateer>.  For
occasional use, you can just run it and add its output to your httpd.conf
file:

    $ templateer vhost.tpl httpd.conf
    Reading template vhost.tpl...
    Kindly provide the values for the following template values:
    DOMAIN = example.com
    VIP_ADDR = 10.0.0.1
    Writing to httpd.conf...
    DOMAIN => example.com
    VIP_ADDR => 10.0.0.1

If you had a lot of hosts to add and you could put the names into a file and
then use a shell loop to run B<templateer> for each one, putting (for
example) each configuration into a separate file in conf.d:

    $ export VIP_ADDR=10.0.0.1

    $ for vhost in $(cat new_vhosts); do \
        DOMAIN=$vhost templateer vhost.tpl conf.d/${vhost}.conf \
    done

Notice that shells let you set variables for each process if you start the
command line with the VAR=val and that the variable only lasts as long as
the process lasts.

=head1 AUTHOR

    Wil Cooley <wcooley@nakedape.cc>

=cut
